

CREATE VIEW [FDV].[VW_D_SKU]
AS

WITH Generic_Name as
/* Can be created by using ORDER_LINE_ITEM although you would mis items never ordered (new)*/
(
SELECT			  I.ITEM_NAME			AS SKU_Name
				, I.DESCRIPTION			AS SKU_Description
				, CASE 
					WHEN LEFT(I.ITEM_NAME, 3)='LFD'  THEN 'Label'
					WHEN LEFT(I.ITEM_NAME, 3)='PDF'  THEN 'PDF'
					WHEN IsNumeric(SUBSTRING(I.ITEM_NAME,1,6)) = 1 THEN SUBSTRING(I.ITEM_NAME,1,6)
					WHEN IsNumeric(SUBSTRING(I.ITEM_NAME,1,5)) = 1 THEN SUBSTRING(I.ITEM_NAME,1,5)
					WHEN IsNumeric(SUBSTRING(I.ITEM_NAME,1,4)) = 1 THEN SUBSTRING(I.ITEM_NAME,1,4)
					WHEN IsNumeric(SUBSTRING(I.ITEM_NAME,1,3)) = 1 THEN SUBSTRING(I.ITEM_NAME,1,3)  
					WHEN IsNUmeric(RIGHT(I.ITEM_NAME,5)) = 1 THEN RIGHT(I.ITEM_NAME,5)
					WHEN IsNUmeric(RIGHT(I.ITEM_NAME,4)) = 1 THEN RIGHT(I.ITEM_NAME,4)
					WHEN IsNUmeric(RIGHT(I.ITEM_NAME,3)) = 1 THEN RIGHT(I.ITEM_NAME,3)
					WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,5),1,4)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,5),1,4)
					WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,4),1,3)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,4),1,3)
					WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,6),1,4)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,6),1,4)
					WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,5),1,3)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,5),1,3)
					WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,7),1,4)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,7),1,4)
					WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,6),1,3)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,6),1,3)
					WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,8),1,4)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,8),1,4)
					WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,7),1,3)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,7),1,3)
					ELSE I.ITEM_NAME
					END Generic_SKU_Name
FROM			MANH.ITEM_CBO AS I 
--WHERE I.ITEM_NAME like '188%'
)
, Generic_Description AS
(
	Select			
	  A.SKU_Name A_SKU
	  , B.Generic_SKU_Name
	  , min(B.SKU_Description) Generic_SKU_Description 
	FROM			Generic_Name AS A
	JOIN			Generic_Name AS B
	ON				A.Generic_SKU_Name = B.Generic_SKU_Name
	group by A.SKU_Name , B.Generic_SKU_Name
)

SELECT			
	I.ITEM_ID						AS SKU_Code
	, I.ITEM_NAME					AS SKU_Name
	, INVP.PART_PRODUCT_FAMILY		AS FAM_Prod_ID
	, I.DESCRIPTION					AS SKU_Description
	, GI.Generic_SKU_Name			AS Generic_SKU_Name
	, GI.Generic_SKU_Description	AS Generic_SKU_Description
	, I.ITEM_SIZE_DESC				AS SKU_Size_Desc
	, I.ITEM_BAR_CODE				AS SKU_Barcode
	, I.UNIT_WEIGHT					AS Unit_Weight
	, S1.DESCRIPTION				AS Weight_Type
	, I.UNIT_VOLUME					AS Unit_Volume
	, S2.DESCRIPTION				AS Volume_Type
	, I.UNIT_HEIGHT					AS Unit_Height
	, I.UNIT_WIDTH					AS Unit_Width
	, I.UNIT_LENGTH					AS Unit_Length
	, S3.DESCRIPTION				AS Dimension_Type
	, I.VARIABLE_WEIGHT				AS Ind_Variable_Weight
	, UN.UN_NUMBER_VALUE			AS Dangerous_Good
FROM			MANH.ITEM_CBO AS I 
LEFT JOIN		IFS.INVENTORY_PART INVP
ON				I.ITEM_NAME=INVP.PART_NO
AND				INVP.ActInd='Y'
LEFT JOIN		Generic_Description AS GI
ON				I.ITEM_NAME = GI.A_SKU
LEFT JOIN		MANH.SIZE_UOM AS S1 
ON				S1.SIZE_UOM_ID = I.WEIGHT_UOM_ID 
AND				S1.ActInd = 'Y' 
LEFT JOIN		MANH.SIZE_UOM AS S2 
ON				S2.SIZE_UOM_ID = I.VOLUME_UOM_ID 
AND				S2.ActInd = 'Y' 
LEFT JOIN		MANH.SIZE_UOM AS S3 
ON				S3.SIZE_UOM_ID = I.DIMENSION_UOM_ID 
AND				S3.ActInd = 'Y'
LEFT JOIN		MANH.UN_NUMBER AS UN
ON				UN.UN_NUMBER_ID = I.UN_NUMBER_ID
AND				UN.ActInd = 'Y'
WHERE			I.ActInd = 'Y'
UNION
SELECT     - 1, '-1', NULL, 'Unknown', NULL,NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL