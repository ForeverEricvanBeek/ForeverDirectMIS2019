





CREATE VIEW [FD2].[VW_D_SKU]
AS

WITH Generic_Name AS (
	-- Can be created by using ORDER_LINE_ITEM although you would mis items never ordered (new)
	SELECT I.ITEM_NAME		AS SKU_Name
		,I.DESCRIPTION		AS SKU_Description
		,CASE 
			WHEN LEFT(I.ITEM_NAME, 3)='LFD'  THEN 'Label'
			WHEN LEFT(I.ITEM_NAME, 3)='PDF'  THEN 'PDF'
			WHEN IsNumeric(SUBSTRING(I.ITEM_NAME,1,6)) = 1 THEN SUBSTRING(I.ITEM_NAME,1,6)
			WHEN IsNumeric(SUBSTRING(I.ITEM_NAME,1,5)) = 1 THEN SUBSTRING(I.ITEM_NAME,1,5)
			WHEN IsNumeric(SUBSTRING(I.ITEM_NAME,1,4)) = 1 THEN SUBSTRING(I.ITEM_NAME,1,4)
			WHEN IsNumeric(SUBSTRING(I.ITEM_NAME,1,3)) = 1 THEN SUBSTRING(I.ITEM_NAME,1,3)  
			WHEN IsNUmeric(RIGHT(I.ITEM_NAME,5)) = 1 THEN RIGHT(I.ITEM_NAME,5)
			WHEN IsNUmeric(RIGHT(I.ITEM_NAME,4)) = 1 THEN RIGHT(I.ITEM_NAME,4)
			WHEN IsNUmeric(RIGHT(I.ITEM_NAME,3)) = 1 THEN RIGHT(I.ITEM_NAME,3)
			WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,5),1,4)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,5),1,4)
			WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,4),1,3)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,4),1,3)
			WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,6),1,4)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,6),1,4)
			WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,5),1,3)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,5),1,3)
			WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,7),1,4)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,7),1,4)
			WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,6),1,3)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,6),1,3)
			WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,8),1,4)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,8),1,4)
			WHEN IsNUmeric(SUBSTRING(RIGHT(I.ITEM_NAME,7),1,3)) =1 THEN SUBSTRING(RIGHT(I.ITEM_NAME,7),1,3)
			ELSE I.ITEM_NAME
			END			AS Generic_SKU_Name
	FROM MANH.ITEM_CBO AS I
)
, Generic_Description AS (
	SELECT 
		A.SKU_Name						AS A_SKU
		, B.Generic_SKU_Name			AS Generic_SKU_Name
		, MIN(B.SKU_Description)		AS Generic_SKU_Description
	FROM			Generic_Name A
	INNER JOIN		Generic_Name B 
	ON				B.Generic_SKU_Name = A.Generic_SKU_Name
	GROUP BY 
		A.SKU_Name
		, B.Generic_SKU_Name
)

SELECT 
	CONCAT (IP.CONTRACT,IC.ITEM_ID)				AS SKU_Code
	, IC.ITEM_NAME								AS SKU_Name
	, IP.TYPE_CODE								AS SKU_Type_Code
	, CASE WHEN SP.USE_SITE_SPECIFIC_DB = 'TRUE' THEN 'BK' ELSE 'OF' END AS SKU_Order_Type
	, AG.DESCRIPTION							AS SKU_Accounting_Group_Desc
	, ISNULL(IP.COUNTRY_OF_ORIGIN, '-1')		AS SKU_Country_of_Origin
	, IC.DESCRIPTION							AS SKU_Description
	, GI.Generic_SKU_Name						AS SKU_Generic_Name
	, GI.Generic_SKU_Description				AS SKU_Generic_Description
	, IP.PART_PRODUCT_FAMILY					AS SKU_Product_Family_ID
	, IP.SECOND_COMMODITY						AS SKU_Second_Commodity
	, CG.DESCRIPTION							AS SKU_Commodity_Desc
	, IP.CONTRACT								AS SKU_Contract
	, IP.PURCH_LEADTIME							AS SKU_Purchase_LeadTime
	, IP.MANUF_LEADTIME							AS SKU_Manufactured_LeadTime
	, IP.UNIT_MEAS								AS SKU_Unit_Measered
	, IP.ABC_CLASS								AS SKU_ABC_Class
	, IP.PART_STATUS							AS SKU_IFS_Status
	, IP.FREQUENCY_CLASS						AS SKU_Frequency_Class
	, IC.ITEM_SIZE_DESC							AS SKU_Size_Desc
	, IC.ITEM_BAR_CODE							AS SKU_Barcode
	, IC.UNIT_WEIGHT							AS SKU_Weight
	, S1.DESCRIPTION							AS SKU_Weight_Type
	, IC.UNIT_VOLUME							AS SKU_Volume
	, S2.DESCRIPTION							AS SKU_Volume_Type
	, IC.UNIT_HEIGHT							AS SKU_Height
	, IC.UNIT_WIDTH								AS SKU_Width
	, IC.UNIT_LENGTH							AS SKU_Length
	, S3.DESCRIPTION							AS SKU_Dimension_Type
	, IC.VARIABLE_WEIGHT						AS SKU_Ind_Variable_Weight
	, UN.UN_NUMBER_VALUE						AS SKU_Dangerous_Good
FROM			MANH.ITEM_CBO IC
LEFT JOIN		IFS.INVENTORY_PART IP 
ON				IP.PART_NO = IC.ITEM_NAME
AND				IP.ActInd = 'Y'
LEFT JOIN		IFS.ACCOUNTING_GROUP AG 
ON				AG.ACCOUNTING_GROUP = IP.ACCOUNTING_GROUP
AND				AG.ActInd = 'Y'
LEFT JOIN		IFS.SALES_PART SP 
ON				SP.PART_NO = IP.PART_NO
AND				SP.ActInd = 'Y'
LEFT JOIN		IFS.COMMODITY_GROUP CG 
ON				CG.SECOND_COMMODITY = IP.SECOND_COMMODITY
AND				CG.ActInd = 'Y'
LEFT JOIN		Generic_Description AS GI 
ON				GI.A_SKU = IC.ITEM_NAME
LEFT JOIN		MANH.SIZE_UOM AS S1 
ON				S1.SIZE_UOM_ID = IC.WEIGHT_UOM_ID
AND				S1.ActInd = 'Y'
LEFT JOIN		MANH.SIZE_UOM AS S2 
ON				S2.SIZE_UOM_ID = IC.VOLUME_UOM_ID
AND				S2.ActInd = 'Y'
LEFT JOIN		MANH.SIZE_UOM AS S3 
ON				S3.SIZE_UOM_ID = IC.DIMENSION_UOM_ID
AND				S3.ActInd = 'Y'
LEFT JOIN		MANH.UN_NUMBER AS UN 
ON				UN.UN_NUMBER_ID = IC.UN_NUMBER_ID
AND				UN.ActInd = 'Y'
WHERE			IC.ActInd = 'Y'

UNION ALL

SELECT     '-1', '-1', NULL,NULL,NULL,NULL,'Unknown',NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL, NULL