

CREATE VIEW [DM2].[VW_D_Cycle_Count]
AS

WITH FROZEN AS (
SELECT		 PIH.[PHYS_INVN_HDR_ID]		[PHYS_INVN_HDR_ID]
			,PIH.[CREATE_DATE_TIME]		[CREATE_DATE_TIME]	
			,FIH.LOCN_ID				Location_ID_FROZEN
			,ISNULL(ISNULL(FIH.PLT_ID,LPN.TC_PARENT_LPN_ID),-1)					Pallet_ID_FROZEN
			,ISNULL(FIH.CASE_NBR,-1)				LPN_ID_FROZEN
			,ISNULL(ITM.ITEM_NAME,-1)				SKU_Code_FROZEN
			,ISNULL(FID.BATCH_NBR,-1)				Lot_Code_FROZEN
			,ISNULL(FID.INVN_QTY,0)				QUANTITY_FROZEN
  FROM		MANH.[PHYS_INVN_HDR]	PIH
  LEFT JOIN MANH.[FROZN_INVN_HDR] FIH
  ON		PIH.PHYS_INVN_HDR_ID = FIH.PHYS_INVN_HDR_ID
  AND		FIH.ActInd='Y'
  LEFT JOIN MANH.[FROZN_INVN_DTL] FID
  ON		FIH.FROZN_INVN_HDR_ID = FID.FROZN_INVN_HDR_ID
  AND		FID.ActInd='Y'
  LEFT JOIN	MANH.[LPN_INBOUND] LPN
  ON		FIH.LPN_ID = LPN.LPN_ID
  AND		LPN.ActInd='Y'
  LEFT JOIN	MANH.[ITEM_CBO] ITM
  ON		FID.ITEM_ID = ITM.ITEM_ID
  AND		ITM.ActInd='Y'

  WHERE		PIH.ActInd='Y'
  AND		PIH.[COUNT_MODE] = 'T'
  AND		PIH.STAT_CODE = '90'

  ) 
  ,COUNTED AS (
/*---------------------------------------------------------------------------*/
/* Loads Counted Data						                                   */
/*---------------------------------------------------------------------------*/

SELECT		distinct PIH.[PHYS_INVN_HDR_ID]
			,PIH.[CREATE_DATE_TIME]
			,CIH.LOCN_ID					Location_ID_COUNT
			,ISNULL(CIH.PLT_ID,-1)			Pallet_ID_COUNT
			,ISNULL(CIH.CASE_NBR,-1)		LPN_ID_COUNT
			,ISNULL(ITM.ITEM_NAME,-1)		SKU_Code_COUNT
			,ISNULL(CID.BATCH_NBR,-1)		Lot_Code_COUNT
			,ISNULL(CID.INVN_QTY,0)			QUANTITY_COUNTED
			,CID.USER_ID
  FROM		MANH.[PHYS_INVN_HDR]	PIH
  LEFT JOIN MANH.[CNT_INVN_HDR]	CIH
  ON		PIH.PHYS_INVN_HDR_ID = CIH.PHYS_INVN_HDR_ID
  AND		CIH.ActInd='Y'
  LEFT JOIN MANH.[CNT_INVN_DTL]	CID
  ON		CIH.CNT_INVN_HDR_ID = CID.CNT_INVN_HDR_ID
  AND		CID.ActInd='Y'
  LEFT JOIN	MANH.[LPN_INBOUND] LPN
  ON		CIH.LPN_ID = LPN.LPN_ID
  AND		LPN.ActInd='Y'
  LEFT JOIN	MANH.[ITEM_CBO] ITM
  ON		CID.ITEM_ID = ITM.ITEM_ID
  AND		ITM.ActInd='Y'
  WHERE		PIH.ActInd='Y'
  AND		PIH.[COUNT_MODE] = 'T'
  AND		PIH.STAT_CODE = '90'
  ) 
    
  select ISNULL( FROZEN.[PHYS_INVN_HDR_ID],COUNTED.[PHYS_INVN_HDR_ID])			Cycle_Count_Physical_Inventory_ID
		, isnull(FROZEN.Location_ID_FROZEN  , COUNTED.Location_ID_COUNT)		Cycle_Count_Location_ID
		, isnull(FROZEN.Pallet_ID_FROZEN  , COUNTED.Pallet_ID_COUNT)			Cycle_Count_Pallet_ID
		, isnull(FROZEN.LPN_ID_FROZEN  , COUNTED.LPN_ID_COUNT)					Cycle_Count_LPN_ID
		, isnull(FROZEN.SKU_Code_FROZEN  , COUNTED.SKU_Code_COUNT)				Cycle_Count_SKU_Code
		, isnull(FROZEN.Lot_Code_FROZEN  , COUNTED.Lot_Code_COUNT )				Cycle_Count_Lot_Code
		, isnull(FROZEN.CREATE_DATE_TIME , COUNTED.CREATE_DATE_TIME )			Cycle_Count_Count_Date
		, QUANTITY_FROZEN														Cycle_Count_Quantity_Frozen
		, QUANTITY_COUNTED														Cycle_Count_Quantity_Counted
		, ISNULL(QUANTITY_COUNTED,0) - isnull(QUANTITY_FROZEN,0)				Cycle_Count_Quantity_Diff
		
  from FROZEN
   FULL OUTER JOIN COUNTED
   ON  FROZEN.[PHYS_INVN_HDR_ID]=COUNTED.[PHYS_INVN_HDR_ID]
   AND FROZEN.Location_ID_FROZEN  = COUNTED.Location_ID_COUNT
   AND FROZEN.Pallet_ID_FROZEN  = COUNTED.Pallet_ID_COUNT
   AND FROZEN.LPN_ID_FROZEN  = COUNTED.LPN_ID_COUNT
   AND FROZEN.SKU_Code_FROZEN  = COUNTED.SKU_Code_COUNT
   AND FROZEN.Lot_Code_FROZEN  = COUNTED.Lot_Code_COUNT
UNION ALL
SELECT '-1','-1','-1','-1','-1','-1',NULL,NULL,NULL,NULL